from dpipe.config import get_paths


DO = get_paths()['do']

CONFIG_ARG = '--config_path ../resources.config'
SAVED_MODEL = 'model'
TF_LOG = 'train_logs'

TRAIN_IDS = 'train_ids.json'
VAL_IDS = 'val_ids.json'
TEST_IDS = 'test_ids.json'

TEST_DICES = 'test_dices.json'

rule all:
    input:
        SAVED_MODEL, TEST_DICES

rule train_model:
    input:
        TRAIN_IDS, VAL_IDS
    output:
        SAVED_MODEL, TF_LOG
    shell:
        'python {DO} train_model {CONFIG_ARG} --train_ids_path {TRAIN_IDS} --val_ids_path {VAL_IDS} --log_path {TF_LOG} --save_model_path {SAVED_MODEL}'

rule predict:
    input:
        SAVED_MODEL,
        ids = '{sample}_ids.json'
    output:
        predictions = '{sample}_segm_pred_prob'
    shell:
        'python {DO} predict {CONFIG_ARG} --ids_path {input.ids} --output_path {output.predictions} --restore_model_path {SAVED_MODEL}'

rule predictions2msegm:
    input:
        predictions = '{sample}_segm_pred_prob'
    output:
        predictions = '{sample}_msegm_pred'
    shell:
        'python {DO} predictions2msegm {CONFIG_ARG} --input_path {input.predictions} --output_path {output.predictions}'

rule compute_dices:
    input:
        predictions = '{sample}_msegm_pred'
    output:
        dices = '{sample}_dices.json'
    shell:
        'python {DO} compute_dices {CONFIG_ARG} --predictions_path {input.predictions} --dices_path {output.dices}'
